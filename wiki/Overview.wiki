#summary An Overview of the Antelope language
= Table of Contents =
 * == [Overview#General_Features General Features] ==
 * == [Overview#Functions_&_Methods Functions & Methods] ==
 * == [Overview#Structs,_Cofuncs,_Interfaces Structs, Cofuncs, Interfaces] ==
 * == [Overview#The_Antelope_Environment The Antelope Environment] ==
 * == [Overview#Final_Modifications Final Modifications] ==

<br>
----
= General Features =
----
<br>

== Variable Declarations ==

|| *Primitive Types* || _One Byte_ || _Two Bytes_ || || *Modifiers* || _Explanation_ ||
|| _Unsigned Types:_ || byte, char, bool ||uint || || _volatile_ || Always fetch from memory (never assume value). ||
|| _Signed Types:_  || sbyte || int || || _static_ || Share across all contexts (one-time initializations). ||
|| || || || || _const_ || Value cannot be modified once set. ||

{{{
   *int pi;    // pi is a pointer-to-int variable
   int x,y,z;   // Declare int variables x, y, and z
   int a=1, b=2; // Declare and initialize a and b to 1 and 2
   const HALF = 1/2; // Constant expressions are declared without a type
   const byte W = 2; // Constant variables are declared with the type
   volatile int v;   // v will always be fetched from memory

   i := 5;         // Numeric literals are ints by default (i is an int),
   b := (5->byte); // but they can be cast to other types (b is a byte).

   h1 := 0x01;     // Hexadecimal and binary literals default to the
   h2 := 0x0001;   // smallest (unsigned) type for the number of digits
   b1 := 0b1;      // (h1 and b1 are bytes, but h2 and b2 are uints).
   b2 := 0b0000000001;

   p := &x;        // Declare p with an interred type of *int
   q := {'H','I'}; // Declare q with inferred type of [2]char
}}}

Variables are statically allocated (_within_ instructions when possible, which saves space and uses faster instructions); otherwise within "safe-RAM" space (specified with `#allocate`); otherwise directly into the program. Variables can be associated with an explicit assembly-coded address (e.g. `int x @ "asmAddress"` aliases x with an int value at "asmAddress").

== Arrays ==

Array types are declared with square brackets `[]` preceding the base-type. If a size is provided, then the array is statically allocated; otherwise it is stored as a pointer to an array. "Jagged" arrays are constructed as arrays of array-pointers (with the "inner" dimensions omitted). "Rectangular" arrays are stored as a single static allocation, and indicated by listing multiple dimensions between the brackets (e.g. `[x,y,z]`). Array literals are declared with the dimensions omitted, but result in static allocations:
{{{
   // Array types:
      [5]int // (static) array of 5 ints
       []int // pointer to array of ints
      *[]int // pointer to pointer to array of ints
     *[5]int // ILLEGAL! (use []int instead)

    [5,5]int // (static) array of 5x5 (25) ints
     [,5]int // pointer to array of (?x5) ints
      [,]int // ILLEGAL! (provide an inner dimension, or use []int)
    [5][]int // (static) array of 5 []int values
   [5][5]int // ILLEGAL! (use [5,5]int or [5][]int instead)

     arr1 := []int{1,2,3,4,5,6,7,8,9};  // arr1 is this [9]int
     arr2 := [,]int{{1,2},{3,4},{5,6}}; // arr2 is this [3,2]int

     arr3 := &[]int{1,2,3}; // arr3 is given the ADDRESS ("&") of
                            // this [3]int, and is therefor an []int

     arr4 := new [5]int; // arr4 is given the address of a dynamically
                         // allocated [5]int, and is therefor an []int

     arr5 := new []int{1,2,3}; // same deal as with arr4, but with the
                               // array initialized with the given values

   // Rectangular arrays can be represented as linear arrays:
   [X,Y,Z]int arr; // An XxYxZ array of ints
    [,Y,Z]int p3 = arr; // Pointer to an (?)xYxZ array (3D)
      [,Z]int p2 = arr; // Pointer to an (?)xZ array (2D)
        []int p1 = arr; // Pointer to an (?) array (1D)
   p3[a,b,c] == p2[a*Y+b, c] == p1[(a*Y+b)*Z+c] == arr[a,b,c]
   // NOTE ALSO: arr[i] == p1[i]; arr[i,j] == p2[i,j]; etc.

   // Strings are represented as character arrays:
   str1 := "Null";  // null terminated: []char{'N','u','l','l',0};
   str2 := b"Byte"; // byte prefixed: []char{4,'B','y','t','e'};
   str3 := i"Int";  // int prefixed: []char{3,0,'I','n','t'}; ("3,0" because z80 is little-endian)
   str4 := r"RAW!"; // raw string: []char{'R','A','W','!'};
}}}

== Tuples ==

Antelope only provides "tuples" in the loose sense that parenthesis can be used to group values together _before_ they are operated on (i.e. you can operate on a "tuple of values", but a tuple is not a datatype in itself). Thus ((A,B),C) is the same as (A,B,C) in the same manner that ((A+B)+C) is the same as (A+B+C):
{{{
   byte a, b = c, d;     // byte a, (b = c), d;
   byte (a, b) = (c, d); // byte a = c, b = d;
   byte a = (((b)));     // byte a = b; (as expected)
  (a,b,c) = (x,y,z);     // a=x; b=y; c=z;
  (a,b,c) = ((x,y),z);   // (same as above)
  (x,y,z) = arr[5..8];   // (x,y,z) = (arr[5],arr[6],arr[7]);
}}}

== Enumerations ==

Enums provide generic label-values, and are designed to be very efficient for use with switches:

 * An enum may not contain more than 256 values (which are bytes)
 * Enum values can be compared and assigned, but the numeric representation is hidden (and predetermined for use with look-up tables)
 * A look-up-table is used for fast jumping when an enum is used in a switch:

{{{
   enum Letter {A, B, C, D, E}
   Letter l1 = Letter.A;
   Letter l2 = l1;

   if(l1 > l2) { ... }

   switch(l1) {
      case Letter.A: ...
      case Letter.B: ...
      case Letter.C: ...
   }

   /* --The Assembly--
    * ld hl,cases   ;3
    * ld bc,(value) ;4
    * add hl,bc     ;1
    * jp (hl)       ;1
    *cases: 
    * .dw case_A 
    * .dw case_B 
    * .dw case_C
    * .dw END_OF_THE_SWITCH ;D
    * .dw END_OF_THE_SWITCH ;E
    */
}}}

== Switch Variables ==

Switch-variables are used to directly modify the selected value within a switch. This is much more efficient than using an enum value, because each value of a switch variable corresponds directly with the address of a "case" within its corresponding switch (and therefor can be referred to directly by name without any ambiguity). In other words, a switch variables is not _used in a switch_, but it actually _is part of a specific switch_:

{{{
   switch{X,Y,Z} g1 = g1.X; // Values are referred to via switch-name
   switch{X,Y,Z} g2; ... g2 = g2.X; // g1 and g2 are different types!

   g1 copy1 = g1.Y; // The switch variable name is also a datatype!
   g1 = copy1;

   switch(g1) { 
      case X: ... // X refers to THIS case-address when used with g1
      case Y: ... 
      case Z: ... 
      // NO default code, because all cases are explicit
   }

   /* --The Assembly--
    * jp something ;modifying g1 modifies "something" directly
    */
}}}

== Type-Casts ==

A Type-cast tells the compiler to convert something to different datatype. Whenever possible, and actual conversion will be avoided (e.g. the compiler will have an operation performed in such a way that the _result_ is of the correct type; or it may try to "pick out" the relevant portion or "mask out" the irrelevant information). Type-casts are written by placing an arrow and the target type after an expression:
{{{
   int a, b;
   (a + b) -> byte; // add a + b such that the result is a byte
   (a -> byte) + b; // treat a like a byte while adding
   a + (b -> byte); // treat b like a byte while adding
}}}

== Flow-Control Constructs ==

Switches and "else-if" chains are modeled the same way: If there is a clear pattern of values, then one or more look-up-tables are used (with value bounds checking as needed). Otherwise all conditional checks are placed together before all the code-blocks (saves one jump). "While" and "do-while" loops are stored with the conditional checks AFTER the loop body (while loops initially jump to the check after the loop) (saves one jump per iteration):
{{{
   if(X) { A }      // If X, then do A;
   else if(Y) { B } // Otherwise if Y, do B;
   ...              // (as many "else-ifs" as you like);
   else { C }       // Otherwise, do C (all "elses" optional).

   switch(X) {
      case A:   ... break; // Do this code if X==A.
      case B,C: ... break; // Do this code if X==B or X==C.
      case D:   ...  // Fall through to next case (no "break").
      default:  ...  // Default case (optional).
   }

   while(X) { A }     // While X, do A; repeat.
   until(X) { A }     // Until X, do A; repeat.
   do { A };          // Do A; repeat (forever).
   do { A } while(X); // Do A; repeat while X.
   do { A } until(X); // Do A; repeat until X.

   do {          // Equivalent to:
       A         // ::        goto start
    => until(X); // :: top:   B
       B         // :: start: A
   }             // ::        if(!X) goto top

   for(X; Y; Z) { ... } // X; while(Y) { ... Z }
   for(V,A,B,I) { ... } // for(V=A; V<B; V+=I) { ... }
   for(V: arr)  { ... } // for(t,0,length(arr),1) { V = arr[t]; ... }
   for(V: someYieldyCofunc) { ... } // (similar to above)

   outer:
   while(X) {
      while(Y) {
         ...
         break;       // Exit from inner-most loop
         break outer; // Exit from "outer" (the "while(X)")
         continue;       // Continue inner-most loop
         continue outer; // Continue "outer" loop
      }
   }
}}}

== Goto Command ==

The "goto" command causes program execution to jump to the correspondingly labeled part of the program. Labels associate identifiers with locations in the program (indicated by an identifier followed by a colon). The goto command can only be used to jump _forward_ within the same function, but never into a nested context (out of is ok):
{{{
   func main() {
      start:
      if(...) {
         goto start;  // ILLEGAL (backwards)
         goto end;    // Ok (out of "if")
         goto inner;  // ILLEGAL (into "else")
         goto extern; // ILLEGAL (into another function)
      }
      else {
         inner:
      }
      end:
   }

   func another() { extern: }
}}}

== Static Initializations ==

Non-precomputable static initializations are computed at runtime "before" main(). Other "pre-main" code can be specified within a `static { ... }` block. All such code is compiled into a self-falsifying conditional block so that program write-back works as intended. Static class-instances are initialized using the initial-values of each class member (these are also copied into constructors as needed).

== Dynamic Memory Allocation ==

The `new` and `delete` keywords dynamically allocate & deallocate memory, but must be defined via specific function declarations (where "size" is how many bytes to allocate, and "addr" is the address of the (previous) allocation):
{{{
    func new<T>(*T item, byte size): *T { ... } // allocates memory and returns the address.
    func delete<T>(*T item, byte size) { ... } // frees previously allocated memory.
    // NOTE: 'delete' may be omitted for garbage-collecting environments.
}}}
Pointer-values can then be dynamically allocated with initial values in braces, or left unitialized:
{{{
   *byte x = new byte{5}; // initialized to 5
   *byte y = new byte;    // uninitialized
}}}

== Interpreted Aspects ==

Antelope uses data-flow and liveness analysis to predict which values and computations can be precomputed, simplified, or circumvented altogether. This includes storing `final` references as static allocations, deciding when to inline function-code, and removing variables and constructs which can be entirely circumvented.

The $ operator designates entities to be "interpreted" by performing the aforementioned optimizations with a higher priority, and giving a compilation error if such an entity cannot be evaluated at compile-time. The $ operator designates:
 * Variables for which the value must be known whenever it is used.
 * Functions and function-calls which must be inlined and have no runtime computations.
 * Flow-control constructs which must be interpreted (i.e. conditions and jumps removed).

== Inline Assembly ==

The assembly command allows for the inlining of assembly code (`asm "assembly code";`). The addresses of variables and other entities can be inserted into the assembly code by separating each item with commas (e.g. `asm " LD A," someVarNamedThis;`). The `#assembly` directive behaves the same way, except that the associated code comes before anything else in the program (see "File-Level Declarations").

<br>
----
= Functions & Methods =
----
<br>

*Functions* contain a block of code that can be called from anywhere else in the program. Local function values are "saved" in the system stack when necessary to avoid collisions with recursive calls. Values are passed to and returned from functions via registers as determined by the compiler. Functions cannot be declared inside of other functions (_but see "Cofuncs" for an example of *closures*_). Functions are declared with the "func" keyword, followed by the name of the function, followed by its arguments listed in parenthesis. Return values (if any) are indicated with a colon followed by the return type(s):
{{{
   func plain() { ............. } // 0 args, 0 returns: plain();
   func both(int x, y) { ...... } // 2 args, 0 returns: both(5,6);
   func give(int i): byte { ... } // 1 arg,  1 return:  byte b = give(300);
   func two():(char,char) { ... } // 0 args, 2 returns: char (x,y) = two();
}}}

*Default arguments* are special in Antelope, because they are embedded directly within the function as an assignment (rather than being "automatically" inserted into each call). When a default value that is _not_ used, the function is called just _after_ the corresponding default assignment. In addition to saving program space, this allows each "version" of a function to exist in its reduced form:
{{{
   // Defualt values must come "last":
   func x(int a, b=5, c=6) { ... }

   // Internally, x is stored as follows:
   // (This kind of explicit nesting is NOT ACTUALLY LEGAL)
      func x(int a) {
         int b = 5;
         // Execution continues into the next function:
         func x(int a, b) {
            int c = 6;
            // Execution continues into the next function:
            func x(int a, b, c) {
               ...
            }
         }
      }

   x(1,2,3); // Calls the "innermost function" (the "full version")
   x(1,2); // Like x(1,2,6), but actually calls the "middle function"
   x(1); // Like x(1,5,6), but actually calls the "outermost function"

   // Each "version" of the function is even a valid
   // target for function pointers of the same type:

   func(int,int,int) f3 = x; // f3 points to the inner function
   func(int,int) f2 = x;    // f2 points to the middle function
   func(int) f3 = x;       // f3 points to the inner function
}}}
*Function pointers* are declared like nameless functions, followed by a variable name. They can point to named functions or anonymous functions which are declared on the spot:
{{{
   func(int,char):bool w; // w points to a func(int,char):bool
   func():(int,int) x;    // x points to a func():(int,int)
   func(int) y;           // y points to a func(int)
   func() z;              // z points to a func()
}}}

*Lambda Expressions* are used to declare anonymous functions that function-pointers can point to. Argument types, return types, and even the arguments themselves can be omitted when the context makes them clear. When the function consists of a single return statement, the body can be replaced with "=>" followed by the return-value:
{{{
   (int a,b):int { return a+b; } // Full form
   (a,b) => a+b; // Types removed, body replaced with return value
   func(int a,b):int f = { return a+b; } // All except body removed
   x => 5*x; // Parentheses optional for single argument form
}}}

*Methods* are functions declared with a receiving-type (struct, cofunc, or interface) "dotted" to the function name. Methods are stored as functions with an invisible first argument called "this", which is a pointer to the calling entity. The members of the calling entity can be accessed directly (i.e. "this." is implicit):
{{{
   struct Foo { int x,y; } // see "structs"

   func Foo.divide():int { // Foo.divide is a func(*Foo):int
      return x/y; // short for this.x/this.y
   }

   Foo f(10,5); // see "structs"
   func(*Foo):int fp = Foo.divide;

   f.divide(); // short for Foo.divide(f)
   fp(f); // this also calls Foo.divide(f)
}}}

*Virtual Methods* are declared as functions within a struct, which are stored in the struct as function-pointers, but act as methods for that struct. Virtual methods may be declared with or without a default function body:
{{{
   struct Foo { // (see "structs")
      int x, y;
      func(*Foo) fp; // plain func-pointer (not a method)
      func vm() { ..code.. }; // same as as fp, but acts as a method
   }

   Foo foo(1, 2, f => f.x + f.y);

   foo.fp(foo); // foo has to pass itself into fp
   foo.mp();    // foo passed implicitly as "this"
}}}

<br>
----
= Structs, Cofuncs, Interfaces =
----
<br>

== Structs ==

Structs are aggregate types which may only contain (non-static) data-members:
{{{
   struct Foo { byte b; int i; } // "Foo" is a {byte, int} allocation
   Foo f(10, 260); // Declare f as a Foo of {10, 260}
   f.b = 7;        // Fields can be accessed/modified individually
}}}

*Default values* may be provided to struct members, as long as all members with default values are at the "end" of the struct. These members may be left out of the initialization list, in which case the default values are used:
{{{
   struct Foo {
      int a, b;
      int sum = a+b+c+d; // default can be an expression
      int c, d = 40, 50;
   }

   Foo f1(1,2,3,4,5); // as stated
   Foo f2(1,2,3,4);   // Foo(1,2,3,4,50)
   Foo f3(1,2,3);     // Foo(1,2,3,40,50)
   Foo f4(1,2);       // Foo(1,2,93,40,50) (1+2+40+50=93)
}}}

Structs "inherit" the members their inner members, but only where such transparent does not create ambiguity. To illustrate this intention explicitly, it is legal to use datatype names as variable names:
{{{
   struct Foo { int a,b; }
   struct Bar { Foo Foo; int c; }

   func Bar.sum():int {
      return a+b+c; // short for this.Foo.a + this.Foo.b + this.c
          // (since Bar does not have an a or b, but Bar.Foo does) 
   }

   Bar b(Foo(1,2),3);
   Foo f = b.Foo;    // Access the "Foo" in b like anything else.
}}}

When multiple members contain members of the same name, or when the containing struct already has a member of that name, then neither can be accessed transparently:
{{{
   struct A { int x; }
   struct B { int x, y; }
   struct C { A A; B B; int y; }

   C c(A(1),B(2,3),4);

   c.x;   // INVALID (is it c.A.x or c.B.x?)
   c.B.x; // 2
   c.A.x; // 1
   c.y;   // 4 (hides c.B.y)
   c.B.y; // 3
}}}

("Bridge" methods will be created automatically so that structs can use methods of their anonymous members as their own, but only when it is _absolutely necessary_ for dynamic behavior.)

== Cofuncs ==

Cofunctions are function-objects which can be called like functions, but store data between calls (like structs). The `yield` command is used to return values, but continue from the same spot on the next call:
{{{
   // Using yield to simulate a generator
   cofunc RotatingSeq(int add): int {
      do { // "do forever" loop
         int last = add; // last will be stored internally
         yield add;
         yield add+last;
         yield add-last; // next call starts back at top
      };
   }

   RotatingSeq r();
   r(1); // yields 1
   r(2); // yields (2+1) = 3
   r(3); // yields (3-1) = 2
   r(4); // (starts over) = 4
}}}

Cofuncs can also contain data like a struct:
{{{
   // Simulating a closure on x
   cofunc Counter { int x; }():int {
      x = x+1;
      return x;
   }

   Counter c(3);
   c(); c(); c(); // 4,5,6
   c.x = 10;
   c(); // 11
}}}

The compiler accomplishes all of this by making the following modifications:
 # The underlying function is a method (takes an implicit "this" pointer to a cofunc-instance).
 # Each cofunc-instance is stored as a tail-call (a "goto") to the underlying function (thus allowing the cofunc to be called _as if it was the function itself_), followed by any information needing to be stored between calls.
 # When the `yield` command is used, the underlying function directly modifies the tail-call in the cofunc to jump to where the function left off (rather than to the start of the function).

== Interfaces ==

Interfaces define datatypes representing any entity which has a certain set of methods and data members. For example, a "Printer" could be defined as anything with a "print" method, and a "Communicator" as anything with a "read" and a "print" method (these can be method-pointers or normal methods):
{{{
   struct X { byte b; }
   struct Y { char c; }

   func X.read(): []char { ... }
   func X.print() { ...code to print b... }
   func Y.print() { ...code to print "Hello!"... }

   iface Communicator {
      func read(): []char;
      func print();
   }

   iface Printer { func print(); }

   func usePrinter(Printer p) {
      p.print();
   }

   func useCom(Communicator c): []char {
      c.print();
      return c.read();
   }

   X x(5);
   T y('Q');
   usePrinter(x); // prints 5
   usePrinter(y); // prints "Hello!"
   msg := useCom(x); // prints 5, returns x.read()

   // Interface instances can even be created directly:

   Printer p = x;
   p.print();
   p = y;
   p.print();
}}}

Interfaces variables contain a pointer to the struct representing the interface (the "this"), and a pointer to an array of method-pointers and data members (or just a pointer to a method, if the interface only use one method). For added flexibility, Antelope allows interfaces to be overloaded with alternate values and methods:
{{{
   struct Imitator { int x, y; }

   func Imitator.blah() { ... }
   func Imitator.flah(): []char { ... }

   Imitator i(1,2);
   Communicator c(i, i.flah, i.blah);
   c.read();  // calls i.flah
   c.print(); // calls i.blah
}}}

<br>
----
= The Antelope Environment =
----
<br>

== Namespaces ==

Namespaces are used to "package" source code under different names, both to prevent "name collisions" and to encapsulate (hide) code that should not be accessed externally. Each source program may have one namespace declared at the top of the file, and multiple files may use the same namespace. Anything from another namespace is accessed with the namespace name "dotted" to the front (unless the file is "using" that namespace, which will then do this automatically). Only things which start with a Capital Letter can be accessed from other namespaces. Namespaces may be "nested":
{{{
   // This file is part of the "Animal" namespace:
   namespace Animal;

   int Dinosaur;
   int monkey; // monkey is not accessible outside of
               // the "Animal" namespace (lowercase).
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
   // This file is part of the "Bird" namespace, which
   // is nested within "Animal" (hence "Animal.Bird"):
   namespace Animal.Bird;

   int Cardinal;
   int dodo = monkey; // Everything in "Bird" is also
                      // "Animal", so this is okay.
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
   // This file is in the "Fruit" namespace:
   namespace Fruit;

   int Apple;
   int banana;
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
   // This file is not part of ANY namespace.
   // However, it "uses" the "Fruit" namespace directly:
   using Fruit;

   void main() {
      int x = Animal.Dinosaur; // Dinosaur is in Animal.
      x = Animal.Bird.Cardinal; // etc.
      x = Animal.monkey; // ILLEGAL: monkey is "hidden".
      x = Apple; // The "using Fruit" at the top makes
                 // this the same as "Fruit.Apple".
      x = banana; // ILLEGAL: banana is nevertheless
                  // "hidden" within its namespace.
   }
}}}

== Preprocessor Directives ==

Preprocessor Directives are "prerequisite" instructions to the compiler, such as including code from other files or conditionally ignoring a section of code. They all begin with `#` to distinguish them from other code. All arguments to a directive must be on the same line, but are otherwise separated by whitespace:

{{{
   #include "filename.ant"  // Include code from another file.

   #define TI_83_PLUS // Define some identifier (as a flag only).

   #if FOO // The following code is only to be included if "FOO"
           // was defined with #define (probably in another file).
      ...
   #endif  // Marks the end of the code for the #if directive.

   #assembly "code" // Insert "code" as assembly code.

   #allocate "place1" 99 // Allocate space at "place1" (up to 99 bytes)

   #allocate "place2" -99 // Allocate at "place2" (grow "down" to 99 bytes)

   #error "Some Message" // Signal an error with the given message.
}}}
The `#allocate` directive specifies an assembly-coded address _outside_ of the program which the compiler _may_ use to store large uninitialized values (such as arrays). Allocation will start at the given address, and grow "upward" to the given byte-limit (or "downward" if the limit is negative).

The `#assembly` directive specifies assembly code which is to be inserted at the "very top" of the program (before the "main" function and before any static initializations). Any number of arguments can be given (string literals are translated directly into assembly code; anything else must correspond to a variable, which is then translated into the assembly address of that variable). Example: `#assembly " LD HL,(" someVariable ")"`

Multiple arguments can be provided to the `#if` using the "and" (`&`), "or" (`|`), "xor" (`^`), and "not" (`!`) operators, which resolve in the same order as they do in other code. These conditionals may also be "nested":
{{{
   #if !FOO // If "FOO" was NOT defined...
      #include "file" // (Only include "file" if "FOO" was not defined)
      #if (A & B) ^ (C | D) // If Either (A and B) or (C or D), but not both...
         ...
      #endif // end of the "inner" #if
   #endif // end of the "outer" #if
}}}

== Compiler Interface ==

Every major aspect of the compiler (tokenization, preprocessing, parse-trees, etc.) will be modularized, serializable, and invokable as an API packaged into a JAR file. The Jar file will be executable: As a program, the compiler will take command-line arguments specifying files to compile, directories to use, namespace and environment related options, and possibly requests to only run certain portions of compilation. This design will allow the compiler to function as a standalone compiler, or to be interfaced with a higher-level editing environment; or to be used as a Java API to design a more complex system that has direct control over the compilation process (for example, what BlueJ does with Java).

<br>
----
= Final Modifications =
=== (Changes which still need to be made to the above material) ===
----
<br>

Need to add examples of templated ("generic") functions and types.

Anonymous functions which refer to (non- static/const) external local variables will have restricted use so as not to "escape" the context of its parent function.

All code is precomputed as much as possible (without unrolling loops or recursive calls). The $ operator Requires something to be interpreted, including loops and recursive calls.

Values will be passed/returned in registers such that any two functions with the same pattern of arguments will use the same registers for them. Function pointers may point to functions with "extra" return values (e.g. func(byte) pointing to func(byte):byte).